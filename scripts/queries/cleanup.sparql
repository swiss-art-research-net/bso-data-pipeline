
PREFIX : <http://www.metaphacts.com/resource/>
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

# Convert xsd:dateTime entitites to xsd:date values
DELETE {
  	?s a xsd:dateTime ;
    	rdfs:label ?oldDateTimeEntity ;
    	?p ?oldDateTimeEntity .
} INSERT {
  GRAPH ?g {
    ?s ?p ?newDateTimeValue .
  }
} WHERE {
  GRAPH ?g {
    ?oldDateTimeEntity a xsd:dateTime ;
      rdfs:label ?oldDateTimeValue .
    ?s ?p ?oldDateTimeEntity .
  }
  BIND(STRDT(SUBSTR(STR(?oldDateTimeValue),0,10), xsd:date) as ?newDateTimeValue)
};

# Convert Point statements to wkt:literals
DELETE {
  ?place crm:P168_place_is_defined_by ?oldPointStatement .
} INSERT {
  GRAPH ?g {
    ?place crm:P168_place_is_defined_by ?newPointStatement .
  }
} WHERE {
  GRAPH ?g {
    ?place crm:P168_place_is_defined_by ?oldPointStatement .
  }
  BIND(STRDT(?oldPointStatement, <http://www.opengis.net/ont/geosparql#wktLiteral>) as ?newPointStatement)
};

# Delete open end time statements
DELETE {
    ?s crm:P82a_begin_of_the_begin ?begin .
} WHERE {
    ?s a crm:E52_Time-Span .
    ?s crm:P82a_begin_of_the_begin ?begin .
    FILTER(STR(?begin) = "0-01-01")
};

DELETE {
    ?s crm:P82b_end_of_the_end ?end .
} WHERE {
    ?s a crm:E52_Time-Span .
    ?s crm:P82b_end_of_the_end ?end .
    FILTER(STR(?end) = "9999-12-31")
};