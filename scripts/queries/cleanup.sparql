
PREFIX : <http://www.metaphacts.com/resource/>
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX search: <https://platform.swissartresearch.net/search/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

# Convert Point statements to wkt:literals
DELETE {
  ?place crm:P168_place_is_defined_by ?oldPointStatement .
} INSERT {
  GRAPH ?g {
    ?place crm:P168_place_is_defined_by ?newPointStatement .
  }
} WHERE {
  GRAPH ?g {
    ?place crm:P168_place_is_defined_by ?oldPointStatement .
  }
  BIND(STRDT(STR(?oldPointStatement), <http://www.opengis.net/ont/geosparql#wktLiteral>) as ?newPointStatement)
  FILTER(?oldPointStatement !=?newPointStatement)
};

# Delete open end time statements
DELETE {
    ?s crm:P82a_begin_of_the_begin ?begin .
} WHERE {
    ?s a crm:E52_Time-Span .
    ?s crm:P82a_begin_of_the_begin ?begin .
    FILTER(STRSTARTS(?begin, '0-01-01'))
};

DELETE {
    ?s crm:P82b_end_of_the_end ?end .
} WHERE {
    ?s a crm:E52_Time-Span .
    ?s crm:P82b_end_of_the_end ?end .
    FILTER(STRSTARTS(?end, '9999'))
};

# Delete links to empty GND identifier
DELETE {
  ?s ?p <https://d-nb.info/gnd/> .
} WHERE {
  ?s ?p <https://d-nb.info/gnd/> .
};

DELETE {
  <https://d-nb.info/gnd/> ?p ?o .
} WHERE {
  <https://d-nb.info/gnd/> ?p ?o .
};

# Delete incorrect data
DELETE DATA{
  <https://resource.swissartresearch.net/actor/ACFADC4D-0762-37C5-A0A9-DCB07D4770F2> rdfs:label 'Moritz, Friedrich Wilhelm' .
};

# Merge duplicate entries
#   When entries are duplicates in GND and different identifiers are used in the data,
#   this results in separate entities for the same entity in our platform.
#   If this is a result of the data obtained from the sources, we merge them in post-processing
#   as we want to avoid changing source data that may be overwritten with future updates
DELETE {
  ?secondary ?p1 ?o1 .
  ?s2 ?p2 ?secondary .
} INSERT {
  GRAPH ?g1 {
    ?primary ?p1 ?o1 .
  }
  GRAPH ?g2 {
    ?s2 ?p2 ?primary .
  }
} WHERE {
  GRAPH ?g1 {
    ?secondary ?p1 ?o1 .
  }
  GRAPH ?g2 {
    ?s2 ?p2 ?secondary .
  }
  VALUES(?primary ?secondary ?label) {
    (<https://resource.swissartresearch.net/actor/E31E74B9-5038-3884-B046-F63C8DD473E3> <https://resource.swissartresearch.net/actor/36187FE6-BE8A-3204-A03F-8621E693A3B1> 'Burri, Johann Ulrich')
    (<https://resource.swissartresearch.net/actor/B3494F86-ACF1-3D28-956A-184F8319DE34> <https://resource.swissartresearch.net/actor/5F5DFE27-F3C1-3624-9FE1-CC61F08F0D8C> 'Fischer, Heinrich')
    (<https://resource.swissartresearch.net/actor/BCC4B25E-D416-3F4D-B76A-0E9A08BA45B5> <https://resource.swissartresearch.net/actor/D3913635-B17B-30C5-B10C-FA17A0FD2316> 'Gmelin, Wilhelm Friiedrich')
    (<https://resource.swissartresearch.net/actor/C040769A-079C-3A8E-9B81-C4E88369B4EC> <https://resource.swissartresearch.net/actor/304F417A-743B-349F-881A-38CB3B1C828E> 'Hofer, Andreas')
    (<https://resource.swissartresearch.net/actor/4DB4C258-131B-3543-93D3-9B51C4EDB2BA> <https://resource.swissartresearch.net/actor/C684B599-0197-3F30-8730-39A16C007808> 'Honegger, Johann Jakob')
    (<https://resource.swissartresearch.net/actor/78A03582-6911-32F9-9027-54BF48220C39> <https://resource.swissartresearch.net/actor/F5EC7465-8769-3894-9682-BAB9F76BBB3C> 'L\'Evêque, Henri')
    (<https://resource.swissartresearch.net/actor/B553C476-E551-3E5F-98E5-0DF6D2207BA9> <https://resource.swissartresearch.net/actor/4CA28C47-7769-3AB0-9B3D-88430453E315> 'Leu, Hans')
    (<https://resource.swissartresearch.net/actor/3B76B513-4952-3B62-922B-E8483D664F63> <https://resource.swissartresearch.net/actor/0CC6407A-48BA-3745-AF1B-D9318733D253> 'Maurer, Heinrich')
    (<https://resource.swissartresearch.net/actor/F1B918FA-D3E9-3D65-84FC-BBB8B468CE7E> <https://resource.swissartresearch.net/actor/FB61FE3A-F499-34EC-840E-9CAF99CCB9A2> 'Weber Johannes')
    (<https://resource.swissartresearch.net/actor/9DF4D3E5-6A8E-38EA-AAC8-DA0A88574825> <https://resource.swissartresearch.net/actor/777436B2-949A-3BD1-ACFE-9C71FCB4ACF3> 'Wolf, Caspar')
    (<https://resource.swissartresearch.net/actor/F28B9306-FC34-3EA0-8D28-9E4C7785DC17> <https://resource.swissartresearch.net/actor/44A35736-D202-3840-801C-FEDDC9340A43> 'Möllinger, Louise Franziska')
    (<https://resource.swissartresearch.net/actor/16D80A02-BF4C-3DB2-87F8-B5C9EE78BF4B> <https://resource.swissartresearch.net/actor/E35BC0A7-8F1C-3701-A4DF-C1BBBD23721F> 'Müller X.A.' )
    (<https://resource.swissartresearch.net/actor/1D423C4A-93C6-339A-A24C-8A6875AB49DE> <https://resource.swissartresearch.net/actor/617EC792-D447-3945-A9BB-9E4FEB11CCF4> 'Muller, Théodore (1819 - 1879)')
    (<https://resource.swissartresearch.net/actor/E73D97CB-4CF5-3FE8-B1C2-BDF8D32D2C2C> <https://resource.swissartresearch.net/actor/2C34FA7E-4D78-3282-865E-2234FC688D8B> 'Ruff u. Hofer')
    (<https://resource.swissartresearch.net/actor/7366938B-0732-364B-B155-88DB2F70F795> <https://resource.swissartresearch.net/actor/DBDF55B5-7CB2-360A-B12C-8B84CBAF34C6> 'Rieter, Heinrich')
    (<https://resource.swissartresearch.net/actor/51B3AE97-EEB1-315B-9057-3204003B920C> <https://resource.swissartresearch.net/actor/6B8798EB-E262-3228-A48B-191C76AC810B> 'Oberkogler, Georg Christoph Friedrich')
    (<https://resource.swissartresearch.net/actor/6D02DDAA-B8FE-3678-A2A6-C157D8A5652D> <https://resource.swissartresearch.net/actor/45D34C28-76C0-3057-8821-F290716F511D> 'Pfenninger, Matthias')
    (<https://resource.swissartresearch.net/actor/C783BB53-A0FE-327A-A95E-3083F8BBBBFC> <https://resource.swissartresearch.net/actor/0158E007-77A3-3EAA-AA1B-F6B7E39A8DB9> 'Schellenberg, Johann Ulrich')
  }
};

# Delete persons that are not linked to any other entity
DELETE {
    ?person ?p1 ?o1  .
    ?s2 ?p1 ?person .
} WHERE {
  ?person a search:Person .
  FILTER NOT EXISTS {
    ?thing crm:P14_carried_out_by|crm:P138_represents|crm:P23_transferred_title_from ?person
  }
  {
    ?person ?p1 ?o1 .
  } UNION {
    ?s2 ?p1 ?person .
  }
}