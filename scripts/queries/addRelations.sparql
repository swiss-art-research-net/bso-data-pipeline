PREFIX sari: <http://platform.swissartresearch.net/>
PREFIX search: <http://platform.swissartresearch.net/search/>
PREFIX : <http://www.metaphacts.com/resource/>
PREFIX crmdig: <http://www.ics.forth.gr/isl/CRMdig/>
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX gndo: <https://d-nb.info/standards/elementset/gnd#>

DROP GRAPH sari:relations;

INSERT {
  GRAPH sari:relations {
    ?place crm:P89_falls_within ?parentPlace .
  }
}WHERE {
  ?gndPlace gndo:geographicAreaCode ?gndParentPlace .
  ?place crmdig:L54_is_same-as ?gndPlace .
  ?parentPlace crmdig:L54_is_same-as ?gndParentPlace .
};

INSERT DATA {
  GRAPH sari:relations {
    search:Object rdfs:label 'Object' .
    search:Person rdfs:label 'Person' .
    search:Place rdfs:label 'Place' .
    search:Type rdfs:label 'Type' .
  }
};
 
INSERT {
  GRAPH sari:relations {
      ?subject a search:Person .
  }
} WHERE {
  VALUES(?actor_or_person) {
    (crm:E39_Actor)
    (crm:E21_Person)
  }
  ?subject a ?actor_or_person .
  FILTER(REGEX(STR(?subject), 'swissartresearch.net'))
};

INSERT {
  GRAPH sari:relations {
    ?subject a search:Type .
  }
} WHERE {
  ?subject a crm:E55_Type
  FILTER(REGEX(STR(?subject), 'swissartresearch.net'))
};

INSERT {
  GRAPH sari:relations {
    ?subject a search:Place .
  }
} WHERE {
  ?subject a crm:E53_Place
  FILTER(REGEX(STR(?subject), 'swissartresearch.net'))
};

INSERT {
  GRAPH sari:relations {
    ?subject a search:Object .
  }
} WHERE {
  ?subject a crm:E22_Man-Made_Object
  FILTER(REGEX(STR(?subject), 'swissartresearch.net/artwork'))
};

INSERT DATA {
  GRAPH sari:relations {
    search:artist a crm:E55_Type ;
      rdfs:label 'artist' .
  }
};

INSERT {
  GRAPH sari:relations {
    ?type crmdig:L54_is_same-as search:artist .
  }
} WHERE {
  ?creation crm:P9_consists_of ?subcreation .
  ?subcreation crm:P2_has_type ?type .
  VALUES (?type) {
    (<https://resource.swissartresearch.net/type/MalerIn>)
    (<https://resource.swissartresearch.net/type/ZeichnerIn>)
    (<https://resource.swissartresearch.net/type/disegnatore>)
    (<https://resource.swissartresearch.net/type/pittore>)
    (<https://resource.swissartresearch.net/type/peintre>)
    (<https://resource.swissartresearch.net/type/desinateur>)
    (<https://resource.swissartresearch.net/type/FotografIn>)
    (<https://resource.swissartresearch.net/type/Malerin>)
    (<https://resource.swissartresearch.net/type/Fotograf>)
    (<https://resource.swissartresearch.net/type/dessinteur>)
    (<https://resource.swissartresearch.net/type/dessinateur%3F>)
    (<https://resource.swissartresearch.net/type/art>)
    (<https://resource.swissartresearch.net/type/aut>)
    (<https://resource.swissartresearch.net/type/cre>)
    (<https://resource.swissartresearch.net/type/art%2C%20prm>)
    (<https://resource.swissartresearch.net/type/art%2C%20prm>)
    (<https://resource.swissartresearch.net/type/pht>)
    (<https://resource.swissartresearch.net/type/pht%2C%20edt>)
    (<https://resource.swissartresearch.net/type/art%2C%20art>)
    (<https://resource.swissartresearch.net/type/art%2C%20plt>)
    (<https://resource.swissartresearch.net/type/art%2C%20pht>)
    (<https://resource.swissartresearch.net/type/art%2C%20cmp>)
    (<https://resource.swissartresearch.net/type/art%2C%20cre>)
    (<https://resource.swissartresearch.net/type/pht%2C%20art>)
    (<https://resource.swissartresearch.net/type/art%2C%20pht%2C%20art>)
    (<https://resource.swissartresearch.net/type/com%2C%20pht>)
    (<https://resource.swissartresearch.net/type/pht%2C%20cre>)
  }
};

INSERT {
	GRAPH ?g {
      ?event crm:P14_carried_out_by ?actor .
  }
} WHERE {
  GRAPH ?g {
    ?event crm:P9_consists_of ?subevent .
    ?subevent crm:P14_carried_out_by ?actor .
    FILTER NOT EXISTS { ?event crm:P14_carried_out_by ?actor . }
  }
}