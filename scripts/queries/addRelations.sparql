PREFIX sari: <https://platform.swissartresearch.net/>
PREFIX search: <https://platform.swissartresearch.net/search/>
PREFIX : <http://www.metaphacts.com/resource/>
PREFIX crmdig: <http://www.ics.forth.gr/isl/CRMdig/>
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX gndo: <https://d-nb.info/standards/elementset/gnd#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX frbroo: <http://iflastandards.info/ns/fr/frbr/frbroo/>
PREFIX rds: <http://schema.swissartresearch.net/ontology/rds#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX gn: <http://www.geonames.org/ontology#>
PREFIX schema: <http://schema.org/>

DROP GRAPH sari:relations;

INSERT {
  GRAPH sari:relations {
    ?place crm:P89_falls_within ?parentPlace .
  }
}WHERE {
  {
    ?place gn:featureCode gn:A.ADM2 ;
      gn:parentADM1 ?parentPlace .
  } UNION {
    ?place gn:featureCode gn:A.ADM3 ;
      gn:parentADM2 ?parentPlace .
  } UNION {
    ?place gn:featureCode gn:A.ADM3 ;
      gn:parentADM1 ?parentPlace .
    FILTER NOT EXISTS {
      ?place gn:parentADM2 ?parentDistrict .
    }
  }
};

INSERT {
  GRAPH sari:relations {
    ?parentPlace crm:P89_falls_within ?grandParentPlace .
  }
} WHERE {
  ?place crm:P89_falls_within ?parentPlace .
  ?place gn:parentADM2 ?parentPlace ;
    gn:parentADM1 ?grandParentPlace .
  FILTER NOT EXISTS {
    ?place crm:P89_falls_within ?grandParentPlace .
  }
};

INSERT {
  GRAPH sari:relations {
    ?place crmdig:L54_is_same-as ?swisstopoPlace .
  }
} WHERE {
  ?swisstopoPlace a schema:AdministrativeArea ;
    crmdig:L54_is_same-as ?linkPlace .
  ?place a crm:E53_Place ;
    crmdig:L54_is_same-as ?linkPlace .
};


INSERT DATA {
  GRAPH sari:relations {
    search:Entity a rdfs:Class ;
      rdfs:equivalentClass crm:E1_CRM_Entity ;
      rdfs:label 'Search Entity' .
    search:Object rdfs:label 'Object' ;
      a rdfs:Class ;
      rdfs:subClassOf search:Entity ;
      rdfs:equivalentClass crm:E22_Human-Made_Object .
    search:Person rdfs:label 'Person' ;
      a rdfs:Class ;
      rdfs:subClassOf search:Entity ;
      rdfs:equivalentClass crm:E39_Actor, rds:Person .
    search:Place rdfs:label 'Place'  ;
      a rdfs:Class ;
      rdfs:subClassOf search:Entity ;
      rdfs:equivalentClass crm:E53_Place, rds:Place  .
    search:Type rdfs:label 'Type' ;
      a rdfs:Class ;
      rdfs:subClassOf search:Entity ;
      rdfs:equivalentClass crm:E55_Type, rds:Type .
    search:Event rdfs:label 'Event' ;
      a rdfs:Class ;
      rdfs:subClassOf search:Entity ;
      rdfs:equivalentClass crm:E5_Event, rds:Event .
    search:BibliographicItem rdfs:label 'Bibliographic Item' ;
      a rdfs:Class ;
      rdfs:subClassOf search:Entity ;
      rdfs:equivalentClass frbroo:F24_Publication_Expression, rds:BibliographicItem .
  }
};
 
INSERT {
  GRAPH sari:relations {
      ?subject a search:Person .
      ?subject a rds:Person .
  }
} WHERE {
  VALUES(?actor_or_person) {
    (crm:E39_Actor)
    (crm:E21_Person)
    (crm:E74_Group)
  }
  ?subject a ?actor_or_person .
  FILTER(REGEX(STR(?subject), 'swissartresearch.net'))
};

INSERT {
  GRAPH sari:relations {
    ?subject a search:Type .
    ?subject a rds:Type .
  }
} WHERE {
  ?subject a crm:E55_Type
  FILTER(REGEX(STR(?subject), 'swissartresearch.net'))
};

INSERT {
  GRAPH sari:relations {
    ?subject a search:Place .
    ?subject a rds:Place .
  }
} WHERE {
  ?subject a crm:E53_Place
  FILTER(REGEX(STR(?subject), 'swissartresearch.net'))
  FILTER NOT EXISTS {
    GRAPH <https://resource.swissartresearch.net/graph/smapshot> {
      ?subject a crm:E53_Place .
    }
  }
};

INSERT {
  GRAPH sari:relations {
    ?place crm:P89_falls_within ?parentPlace .
  }
}WHERE {
  ?place a search:Place ;
    crmdig:L54_is_same-as ?swissTopoPlace .
  ?swissTopoPlace a schema:AdministrativeArea ;
    crm:P89_falls_within* ?parentSwissTopoPlace .
  ?parentPlace crmdig:L54_is_same-as ?parentSwissTopoPlace .
  FILTER(?place != ?parentPlace)
};

INSERT {
  GRAPH sari:relations {
    ?subject a search:Object .
  }
} WHERE {
  ?subject a crm:E22_Human-Made_Object
  FILTER(REGEX(STR(?subject), 'swissartresearch.net/artwork'))
};

INSERT {
  GRAPH sari:relations {
    ?subject a search:BibliographicItem .
  }
} WHERE {
  ?subject a frbroo:F24_Publication_Expression
  FILTER(REGEX(STR(?subject), 'swissartresearch.net/bibliographicitem'))
};

INSERT {
  GRAPH sari:relations {
    ?subject dcterms:date ?value .
  }
} WHERE {
  ?subject a search:Object;
    (crm:P108i_was_produced_by|(crm:P128_carries/crm:P94i_was_created_by))/crm:P4_has_time-span ?date .
  OPTIONAL {
    ?date crm:P82a_begin_of_the_begin ?date_value_begin .
  }
  OPTIONAL {
    ?date crm:P82b_end_of_the_end ?date_value_end .
  }
  BIND(IF(BOUND(?date_value_begin), ?date_value_begin, ?date_value_end) AS ?value)
};

INSERT {
	GRAPH ?g {
      ?event crm:P14_carried_out_by ?actor .
  }
} WHERE {
  GRAPH ?g {
    ?event crm:P9_consists_of ?subevent .
    ?subevent crm:P14_carried_out_by ?actor .
    FILTER NOT EXISTS { ?event crm:P14_carried_out_by ?actor . }
  }
};

# Add creation node for works that miss a creation
# (this node is expected by some forms)
INSERT {
  GRAPH ?g {
    ?work crm:P94i_was_created_by ?creation .
    ?creation a crm:E65_Creation .
  }
} WHERE {
  ?subject a search:Object .
  GRAPH ?g {
      ?subject crm:P128_carries ?work .
      ?work a crm:E36_Visual_Item .
    FILTER NOT EXISTS {
      ?work crm:P94i_was_created_by ?creation .
    }
  }
  BIND(URI(CONCAT(STR(?subject), '/creation')) AS ?creation)
};

# Add author role of creater of text where no role has been specified
INSERT {
  GRAPH ?g {
    ?subcreation crm:P2_has_type <https://iqvoc.swissartresearch.net/_8ab5f6e4> .
  }
} WHERE {
  ?subject a search:Object .
  GRAPH ?g {
      ?subject crm:P128_carries ?text .
      ?text a frbroo:F22_Self-Contained_Expression ;
        crm:P94i_was_created_by/crm:P9_consists_of ?subcreation .
    FILTER NOT EXISTS {
      ?subcreation crm:P2_has_type ?creation_type 
    }
  }
};

INSERT {
  GRAPH ?g {
    ?entity crm:P94i_was_created_by ?creation .
  }
} WHERE {
  GRAPH ?g {
    ?entity frbroo:R24i_was_created_through ?creation .
  }
};

INSERT {
  GRAPH sari:relations {
    ?subject a crm:E55_Type .
  } 
} WHERE {
  ?subject a skos:Concept .
};

INSERT {
  GRAPH sari:relations {
    ?subject crm:P2_has_type ?dossierType ;
      crm:P109_has_current_or_former_curator ?sff .
  }
} WHERE {
  BIND(<https://resource.swissartresearch.net/type/1525E0B2-4816-3D3F-AEC8-94BDE16CF0EC> as ?dossierType)
  BIND(<https://d-nb.info/gnd/1196831858> as ?sff)
  ?child crm:P109_has_current_or_former_curator ?sff ;
    crm:P46i_forms_part_of ?subject .
};

INSERT {
  GRAPH sari:relations {
    ?subject rdfs:label ?langLabel .
  }
} WHERE {
  ?subject a ?type ;
	crmdig:L54_is_same-as/rdfs:label ?langLabel .
  FILTER(lang(?langLabel) = 'en' || lang(?langLabel) = 'fr' || lang(?langLabel) = 'it' || lang(?langLabel) = 'de')
  VALUES (?type) {
    (search:Place)
    (schema:AdministrativeArea)
  }
}