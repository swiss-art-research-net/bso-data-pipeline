PREFIX : <http://www.metaphacts.com/resource/>
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX crmdig: <http://www.ics.forth.gr/isl/CRMdig/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX frbroo: <http://iflastandards.info/ns/fr/frbr/frbroo/>
PREFIX gn: <http://www.geonames.org/ontology#>
PREFIX gndo: <https://d-nb.info/standards/elementset/gnd#>
PREFIX graph: <https://resource.swissartresearch.net/graph/>
PREFIX la: <https://linked.art/ns/terms/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rds: <http://schema.swissartresearch.net/ontology/rds#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rso: <http://www.researchspace.org/ontology/>
PREFIX sari: <https://platform.swissartresearch.net/>
PREFIX schema: <http://schema.org/>
PREFIX search: <https://platform.swissartresearch.net/search/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

DROP GRAPH sari:mahMappings;

# Add BSO-specific types as shortcuts for the basic entity types
INSERT {
  GRAPH sari:mahMappings {
    ?subject a ?bsoType .
  }
} WHERE {
  GRAPH graph:mah {
    ?subject a ?type .
    FILTER EXISTS {
      ?subject crm:P1_is_identified_by ?identifier .
    }
  }
  VALUES (?type ?bsoType) {
    (crm:E22_Human-Made_Object search:Object)
    (crm:E39_Actor search:Actor)
    (crm:E53_Place search:Place)
    (crm:E5_Event search:Event)
  }
};

# Add metadata about the holding collection of the objects
INSERT {
  GRAPH sari:mahMappings {
    ?subject crm:P109_has_current_or_former_curator ?collection .
    ?collection a gndo:CorporateBody, crm:E39_Actor;
      rdfs:label 'MuseÃÅe d\'art et d\'histoire (Genf)' .
  }
} WHERE {
  GRAPH graph:mah {
    ?subject a crm:E22_Human-Made_Object .
    BIND(<https://d-nb.info/gnd/25478-2> as ?collection)
  }
};

# Identify exact matches between entities in the MAH graph and existing BSO entities.
# If a match is found, replace the old entity with the new entity in the MAH graph
# by redirecting all incoming and outgoing relationships.
DELETE {
  GRAPH graph:mah {
    ?oldEntity ?p1 ?o1 .
    ?s2 ?p2 ?oldEntity .
  }
} 
INSERT {
  GRAPH sari:mahMappings {
    ?newEntity ?p1 ?o1 .
    ?s2 ?p2 ?newEntity .
    ?newEntity crmdig:L54_is_same-as ?oldEntity .
  }
} WHERE {
  GRAPH graph:mah {
    ?oldEntity skos:exactMatch ?match .
    ?match a ?type .
  }
  BIND(STRBEFORE(STRAFTER(STR(?match), '://'),'/') AS ?baseURI)
  VALUES (?type ?baseURI) {
    (crm:E39_Actor 'd-nb.info')
    (crm:E53_Place 'geo.ld.admin.ch')
  }
  ?newEntity crmdig:L54_is_same-as ?match .
  ?oldEntity ?p1 ?o1 .
  ?s2 ?p2 ?oldEntity .
};

# Add meta object for visual work as a carried by the E22 Human-Made Object.
# This is for compatibility with the BSO data model.
INSERT {
  GRAPH sari:mahMappings {
    ?subject crm:P128_carries ?work .
    ?work a crm:E36_Visual_Item .
    ?work crm:P138_represents ?something .
    ?something a ?bsoType
  }
} WHERE {
  GRAPH graph:mah {
    ?subject crm:P62_depicts ?something .
    ?something a ?type .
    BIND(URI(CONCAT(STR(?subject), '/bso/work')) AS ?work)
    VALUES (?type ?bsoType) {
      (crm:E39_Actor search:Actor)
      (crm:E53_Place search:Place)
      (crm:E5_Event search:Event)
    }
  }
};

# Add the Linked.Art IIIF paths for the images of the objects
INSERT {
  GRAPH sari:mahMappings {
    ?subject crm:P128_carries ?work .
    ?work a crm:E36_Visual_Item .
    ?work la:digitally_shown_by ?iiif_image .
    ?iiif_image a crmdig:D1_Digital_Object .
    ?iiif_image la:digitally_available_via ?iiif .
    ?iiif a la:DigitalService ;
      dcterms:conformsTo <http://iiif.io/api/image> ;
      la:access_point ?access_point .
    ?access_point a crmdig:D1_Digital_Object, rso:EX_Digital_Image .
    
  }
} WHERE {
  GRAPH graph:mah {
    ?subject a crm:E22_Human-Made_Object ;
      crm:P138i_has_representation ?image .
    ?image rdf:value ?value .
    FILTER(STRSTARTS(?value, 'https://www.mahmah.ch/imagezoom/?IIIF'))
    BIND(URI(STRBEFORE(?value, '/full/')) AS ?access_point)
    BIND(URI(CONCAT(STR(?subject), '/bso/work')) AS ?work)
    BIND(URI(CONCAT(STR(?subject), '/bso/image')) AS ?iiif_image)
    BIND(URI(CONCAT(STR(?subject), '/bso/iiif')) AS ?iiif)
  }
}